group 'com.github.vspr-ai'
version '1.4.0'
description 'another slack api for java'

apply plugin: 'java'
apply plugin: "net.ltgt.apt" // <-- apt plugin
apply plugin: 'idea'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.7"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}
apply plugin: 'io.codearte.nexus-staging'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile 'org.sonatype.goodies:goodies-testsupport:2.2.4'
    testCompile group: 'org.jacoco', name: 'jacoco-maven-plugin', version: '0.7.9'

    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.25.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.24'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.25.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.10'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.8.6'
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'com.google.guava', name: 'guava', version: '21.0'
    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.8.10'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'

    apt "org.immutables:value:2.4.3" // for annotation processing only


    compileOnly "org.immutables:value:2.4.3:annotations" // compilation-only annotations artifact
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply plugin: 'signing'
apply plugin: 'maven'

// Signature of artifacts
signing {
    sign configurations.archives
}

def centralUsername = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername')
def centralPassword = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword')

// OSSRH publication
uploadArchives {
    repositories {
        mavenDeployer {
            // POM signature
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            // Target repository
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: centralUsername, password: centralPassword)
            }
            pom.project {
                name project.name
                description project.description
                packaging 'jar'
                url 'https://github.com/vspr-ai/slack-api'

                scm {
                    connection 'scm:git:https://github.com/vspr-ai/slack-api.git'
                    developerConnection 'scm:git:git@github.com:vspr-ai/slack-api.git'
                    url 'https://github.com/vspr-ai/slack-api.git'
                }

                licenses {
                    license {
                        name 'Apache 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'Cobb'
                        name = 'Eric Cobb'
                        email = 'cobb@vspr.ai'
                    }
                }
            }
        }
    }
}


nexusStaging {
    username = centralUsername
    password = centralPassword
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}